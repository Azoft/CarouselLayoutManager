buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.4.1"
        classpath "com.github.dcendents:android-maven-plugin:1.2"
    }
}

apply plugin: "com.android.library"

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//////////////////////////////// Upload to BinTray ////////////////////////////////
group = "com.azoft.carousellayoutmanager"
final def siteUrl = 'https://github.com/Azoft/CarouselLayoutManager'
final def gitUrl = "https://github.com/Azoft/CarouselLayoutManager.git"
// This is the library version used when deploying the artifact
version = "1.2.1"

final Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ["archives"]
    pkg {
        repo = "maven"
        name = "carousel"
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'

            // Add your description here
            name "CarouselLayoutManager for RecyclerView in Android. Description is here: https://github.com/Azoft/CarouselLayoutManager"
            url siteUrl

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'azoft'
                    name 'azoft'
                    email 'android-mobile@azoft.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }
}
//////////////////////////////// Upload to BinTray ////////////////////////////////

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    resourcePrefix "carousellayoutmanager__"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    configurations {
        javadocDeps
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    javadocDeps 'com.android.support:support-annotations:25.0.0'
    javadocDeps 'com.android.support:recyclerview-v7:25.0.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

// Excluding aar artifacts from uploading
configurations.archives.artifacts.with { archives ->
    for (def iter = archives.iterator(); iter.hasNext();) {
        if (iter.next().file =~ 'aar') iter.remove();
    }
}