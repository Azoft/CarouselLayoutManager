buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.0.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
        classpath "com.github.dcendents:android-maven-plugin:1.2"
    }
}

apply plugin: "com.android.library"

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//////////////////////////////// Upload to BinTray ////////////////////////////////
group = "com.azoft.carousellayoutmanager"
def gitUrl = "https://github.com/Azoft/CarouselLayoutManager.git"
// This is the library version used when deploying the artifact
version = "1.0.2"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ["archives"]
    pkg {
        repo = "maven"
        name = "carousel"
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
        }
    }
}
//////////////////////////////// Upload to BinTray ////////////////////////////////

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    resourcePrefix "carousellayoutmanager__"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    configurations {
        javadocDeps
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    javadocDeps 'com.android.support:support-annotations:23.3.0'
    javadocDeps 'com.android.support:recyclerview-v7:23.3.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

// Excluding aar artifacts from uploading
configurations.archives.artifacts.with { archives ->
    for (def iter = archives.iterator(); iter.hasNext();) {
        if (iter.next().file =~ 'aar') iter.remove();
    }
}